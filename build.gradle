buildscript {
    repositories {
        maven {
            name 'Shadow'
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

archivesBaseName = "K9"
group = 'com.tterrag.k9'
description = "Discord bot with MCP lookups and more"
sourceCompatibility = 1.8
targetCompatibility = 1.8

import org.ajoberstar.grgit.Grgit

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else if (new File(projectDir, '.git').exists()) {
    def repo = Grgit.open(project.file('.'))
    version += ".${repo.log().find().abbreviatedId}"
} else {
    version += ".CUSTOM"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
	from('.') {
		include 'MCDrama/**' 
	}
	manifest {
 	   attributes 'Main-Class': 'com.tterrag.k9.K9',
 	              'Implementation-Version': project.version
	}
}

shadowJar {
	mergeServiceFiles()
	dependencies {
		exclude "*.html"
		exclude dependency('org.projectlombok:lombok')
	}
}

repositories {
    mavenCentral()
	jcenter() 
	maven {
		url  "https://jitpack.io"
	}
}

dependencies {
    compile 'com.discord4j.discord4j:discord4j-core:3.0.0'
    compile 'org.pircbotx:pircbotx:2.0.1'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.guava:guava:17.0'
	compile 'net.sf.trove4j:trove4j:3.0.3'
	compile 'ch.qos.logback:logback-classic:1.2.1'
    compile 'org.projectlombok:lombok:1.18.4'
    compile "com.beust:jcommander:1.71"
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.eclipse.mylyn.github', name: 'org.eclipse.egit.github.core', version: '2.1.5'
    compile group: 'org.jruby', name: 'jruby-complete', version: '9.1.14.0'
    compile group: 'org.clojure', name: 'clojure', version: '1.7.0'
    compile group: 'org.clojure', name: 'math.numeric-tower', version: '0.0.4'
	compile fileTree(dir: 'libs', include:'*.jar')
	
	compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}